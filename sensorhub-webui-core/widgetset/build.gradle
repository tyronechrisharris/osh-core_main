description = 'OSH Vaadin WidgetSet'

configurations {
  vaadin {
    extendsFrom compile
  }
}

dependencies {
  compile 'com.vaadin:vaadin-shared:' + vaadinVersion
  compile 'com.vaadin:vaadin-server:' + vaadinVersion
  compile 'com.vaadin:vaadin-client-compiled:' + vaadinVersion
  compile 'com.vaadin:vaadin-compatibility-client:' + vaadinVersion
  compile 'com.vaadin:vaadin-compatibility-client-compiled:' + vaadinVersion
  compile 'com.vaadin:vaadin-themes:' + vaadinVersion
  vaadin 'com.vaadin:vaadin-client-compiler:' + vaadinVersion 
}

task compileWidgetset(type: JavaExec) {
  def inputSource = sourceSets.main.resources.srcDirs
  def outputFolder = "$projectDir/src/main/resources/VAADIN/widgetsets"
  inputs.dir(inputSource)
  outputs.file(outputFolder)
  
  main 'com.vaadin.tools.WidgetsetCompiler'
  classpath inputSource
  classpath configurations.vaadin
  jvmArgs '-Xmx512M', '-Xss512M', '-Dgwt.persistentunitcachedir='+project.buildDir.canonicalPath
  args '-war', outputFolder
  args '-deploy', "$projectDir/build/deploy"
  args '-optimize', '0'
  args '-localWorkers', '' + Runtime.getRuntime().availableProcessors()
  args '-logLevel', 'WARN'
  args 'org.sensorhub.ui.SensorHubWidgetSet'
  /*doFirst {
    println commandLine
  }*/
}

task compileTheme(type: JavaExec) {
  def inputScss = "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.scss"
  def outputCss = "$projectDir/src/main/resources/VAADIN/themes/sensorhub/styles.css"
  inputs.file(inputScss)
  outputs.file(outputCss)
  
  main 'com.vaadin.sass.SassCompiler'
  classpath sourceSets.main.resources.srcDirs
  classpath configurations.vaadin
  jvmArgs '-Xmx64M', '-Xss64M'
  args inputScss
  args outputCss
  /*doFirst {
    println commandLine
  }*/
}

task cleanVaadin(type: Delete) {
  delete fileTree("$projectDir/src/main/resources/VAADIN/themes") {
      include '**/*.css'
  }
  delete "$projectDir/src/main/resources/VAADIN/widgetsets"
}

processResources.dependsOn compileWidgetset
processResources.dependsOn compileTheme
clean.dependsOn cleanVaadin