description = 'OSH SWE Services'
ext.details = 'Implementation of OGC SWE standard interfaces as OSH services and virtual sensors'

rootProject.allprojects {
  repositories {
    maven { url "http://52north.org/maven/repo/releases" }
  }
}

dependencies {
  compile project(':sensorhub-core')
  compile project(':ogc-services-sos')
  compile project(':ogc-services-sps')
   
  compile('org.n52.amused:amused-core:1.0.0') {
    // excludes are needed here so the generated pom contains them too
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'xerces', module: 'xercesImpl'
  }
  compile 'org.n52.amused:amused-wsn-impl:1.0.0'
  compile 'xml-apis:xml-apis:1.4.01'
  compile 'org.eclipse.jetty.websocket:websocket-server:9.4.7.v20170914'
    
  testCompile project(path: ':sensorhub-core', configuration: 'testArtifacts')
  testCompile project(':sensorhub-storage-perst')
  testCompile 'commons-io:commons-io:1.3.2'
    
  configurations {
    runtime {
       exclude module: 'xalan'
       exclude module: 'xercesImpl'
    }
  }
}

// add info to OSGI manifest
jar {
  manifest {
    instruction 'Bundle-Vendor', 'Sensia Software LLC'
    instruction 'Bundle-Activator', 'org.sensorhub.impl.service.sos.Activator'
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  } 
}
