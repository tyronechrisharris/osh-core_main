description = 'OSH Core APIs & Components'
ext.details = 'Core components of OpenSensorHub framework'

dependencies {
  embeddedApi project(':swe-common-core')
  embeddedApi project(':swe-common-om')
  embeddedApi project(':sensorml-core')
  api 'org.osgi:org.osgi.framework:1.9.0'
  embeddedApi 'org.eclipse.jetty:jetty-servlet:9.4.42.v20210604'
  embeddedApi 'org.eclipse.jetty:jetty-servlets:9.4.42.v20210604'
  embeddedApi 'org.eclipse.jetty.websocket:websocket-server:9.4.42.v20210604'
  embeddedApi 'org.eclipse.jetty:jetty-xml:9.4.42.v20210604'
    
  testImplementation project(path: ':sensorml-core', configuration: 'testArtifacts')
  testImplementation 'commons-io:commons-io:1.3.2'
}

// add info to OSGI manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Sensia Software LLC')
    attributes('Bundle-Activator': 'org.sensorhub.impl.Activator')
    attributes('-exportcontents': '''
       org.sensorhub.*,
       org.vast.*,
       net.opengis.*,
       org.eclipse.jetty.*,
       javax.servlet.*,
       com.google.common.*,
       com.google.gson.*,
       com.vividsolutions.*,
       org.slf4j.*,
       com.rits.*,
       org.objenesis.*
    ''')
    
    // override import packages since this is our root bundle
    attributes('Import-Package': 'org.osgi.framework')
    
    // allow binding to OWS reader/writer dynamically (needed by OGCRegistry)
    attributes('DynamicImport-Package': 'org.vast.ows,org.vast.ows.*')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  } 
}

task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = 'tests'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

configurations {
  testArtifacts
}

artifacts {
  testArtifacts packageTests
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact sourcesJar 
      artifact javadocJar
    }
  }
}
